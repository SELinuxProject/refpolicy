## <summary>PBX software.</summary>

########################################
## <summary>
##	Execute callweaver in the caller domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed access.
## </summary>
## </param>
#
interface(`callweaver_exec',`
	gen_require(`
		type callweaver_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, callweaver_exec_t)
')

########################################
## <summary>
##	Connect to callweaver over a
##	unix stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`callweaver_stream_connect',`
	gen_require(`
		type callweaver_t, callweaver_var_run_t;
	')

	files_search_pids($1)
	stream_connect_pattern($1, callweaver_var_run_t, callweaver_var_run_t, callweaver_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an callweaver environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`callweaver_admin',`
	gen_require(`
		type callweaver_t, callweaver_initrc_exec_t, callweaver_log_t;
		type callweaver_var_lib_t, callweaver_var_run_t, callweaver_spool_t;
	')

	allow $1 callweaver_t:process { ptrace signal_perms };
	ps_process_pattern($1, callweaver_t)

	init_startstop_service($1, $2, callweaver_t, callweaver_initrc_exec_t)

	logging_search_logs($1)
	admin_pattern($1, callweaver_log_t)

	files_search_pids($1)
	admin_pattern($1, callweaver_var_run_t)

	files_search_var_lib($1)
	admin_pattern($1, { callweaver_spool_t callweaver_var_lib_t })
')
