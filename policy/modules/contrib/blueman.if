## <summary>Tool to manage Bluetooth devices.</summary>

########################################
## <summary>
##	Execute blueman in the blueman domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`blueman_domtrans',`
	gen_require(`
		type blueman_t, blueman_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, blueman_exec_t, blueman_t)
')

########################################
## <summary>
##	Send and receive messages from
##	blueman over dbus.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`blueman_dbus_chat',`
	gen_require(`
		type blueman_t;
		class dbus send_msg;
	')

	allow $1 blueman_t:dbus send_msg;
	allow blueman_t $1:dbus send_msg;
')

########################################
## <summary>
##	Search blueman lib directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`blueman_search_lib',`
	gen_require(`
		type blueman_var_lib_t;
	')

	allow $1 blueman_var_lib_t:dir search_dir_perms;
	files_search_var_lib($1)
')

########################################
## <summary>
##	Read blueman lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`blueman_read_lib_files',`
	gen_require(`
		type blueman_var_lib_t;
	')

	files_search_var_lib($1)
	read_files_pattern($1, blueman_var_lib_t, blueman_var_lib_t)
')

########################################
## <summary>
##	Create, read, write, and delete
##	blueman lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`blueman_manage_lib_files',`
	gen_require(`
		type blueman_var_lib_t;
	')

	files_search_var_lib($1)
	manage_files_pattern($1, blueman_var_lib_t, blueman_var_lib_t)
')
