## <summary>Set up, mount/unmount, and delete an swap file.</summary>

########################################
## <summary>
##	Dontaudit acces to the swap file.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`dphysswapfile_dontaudit_read_swap',`
	refpolicywarn(`$0($*) has been deprecated')

	gen_require(`
		type dphysswapfile_swap_t;
	')

	dontaudit $1 dphysswapfile_swap_t:file read_file_perms;
')

########################################
## <summary>
##	All of the rules required to
##	administrate an dphys-swapfile environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`dphysswapfile_admin',`
	gen_require(`
		type dphysswapfile_t, dphysswapfile_conf_t;
		type dphysswapfile_initrc_exec_t, dphysswapfile_unit_t;
	')

	admin_process_pattern($1, dphysswapfile_t)

	init_startstop_service($1, $2, dphysswapfile_t, dphysswapfile_initrc_exec_t, dphysswapfile_unit_t)

	files_search_etc($1)
	admin_pattern($1, dphysswapfile_conf_t)

	# do not grant access to swap file for now
')
