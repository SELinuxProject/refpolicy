## <summary>Policy for logical volume management programs.</summary>

########################################
## <summary>
##	Execute lvm programs in the lvm domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
interface(`lvm_domtrans',`
	gen_require(`
		type lvm_t, lvm_exec_t;
	')

	corecmd_search_sbin($1)
	domain_auto_trans($1, lvm_exec_t, lvm_t)

	allow $1 lvm_t:fd use;
	allow lvm_t $1:fd use;
	allow lvm_t $1:fifo_file rw_file_perms;
	allow lvm_t $1:process sigchld;
')

########################################
## <summary>
##	Execute lvm programs in the lvm domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to allow the LVM domain.
##	</summary>
## </param>
## <param name="terminal">
##	<summary>
##	The type of the terminal allow the LVM domain to use.
##	</summary>
## </param>
## <rolecap/>
#
interface(`lvm_run',`
	gen_require(`
		type lvm_t;
	')

	lvm_domtrans($1)
	role $2 types lvm_t;
	allow lvm_t $3:chr_file rw_term_perms;
')

########################################
## <summary>
##	Read LVM configuration files.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
## <rolecap/>
#
interface(`lvm_read_config',`
	gen_require(`
		type lvm_t, lvm_etc_t;
	')

	files_search_etc($1)
	allow $1 lvm_etc_t:dir r_dir_perms;
	allow $1 lvm_etc_t:file r_file_perms;
')

