## <summary>Policy for needrestart utility.</summary>
##
## <desc>
## Utility to restart daemons after system updates
## </desc>

########################################
## <summary>
##      Execute needrestart in the user role
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed to transition.
##      </summary>
## </param>
## <param name="role">
##      <summary>
##      Role allowed access.
##      </summary>
## </param>
## <rolecap/>
#
interface(`needrestart_run',`
	gen_require(`
		attribute_role needrestart_roles;
		type needrestart_exec_t, needrestart_t;
	')

	domtrans_pattern($1, needrestart_exec_t, needrestart_t)
	roleattribute $2 needrestart_roles;
	systemd_run_passwd_agent(needrestart_t, $2)
')

########################################
## <summary>
##	Execute needrestart programs in the needrestart domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`needrestart_domtrans',`
	gen_require(`
		type needrestart_t, needrestart_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, needrestart_exec_t, needrestart_t)
')

