## <summary>Log analyzer for squid proxy.</summary>

########################################
## <summary>
##	Execute the lightsquid program in
##	the lightsquid domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`lightsquid_domtrans',`
	gen_require(`
		type lightsquid_t, lightsquid_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, lightsquid_exec_t, lightsquid_t)
')

########################################
## <summary>
##	Execute lightsquid in the
##	lightsquid domain, and allow the
##	specified role the lightsquid domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
#
interface(`lightsquid_run',`
	gen_require(`
		attribute_role lightsquid_roles;
	')

	lightsquid_domtrans($1)
	roleattribute $2 lightsquid_roles;
')

########################################
## <summary>
##	All of the rules required to
##	administrate an lightsquid environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`lightsquid_admin',`
	gen_require(`
		type lightsquid_t, lightsquid_rw_content_t;
	')

	allow $1 lightsquid_t:process { ptrace signal_perms };
	ps_process_pattern($1, lightsquid_t)

	lightsquid_run($1, $2)

	files_search_var_lib($1)
	admin_pattern($1, lightsquid_rw_content_t)

	apache_list_sys_content($1)
')
