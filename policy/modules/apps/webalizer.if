## <summary>Web server log analysis</summary>

########################################
## <summary>
##	Execute webalizer in the webalizer domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
interface(`webalizer_domtrans',`
	gen_require(`
		type webalizer_t, webalizer_exec_t;
	')

	domain_auto_trans($1,webalizer_exec_t,webalizer_t)

	allow $1 webalizer_t:fd use;
	allow webalizer_t $1:fd use;
	allow webalizer_t $1:fifo_file rw_file_perms;
	allow webalizer_t $1:process sigchld;
')

########################################
## <summary>
##	Execute webalizer in the webalizer domain, and
##	allow the specified role the webalizer domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the webalizer domain.
##	</summary>
## </param>
## <param name="terminal">
##	<summary>
##	The type of the terminal allow the webalizer domain to use.
##	</summary>
## </param>
## <rolecap/>
#
interface(`webalizer_run',`
	gen_require(`
		type webalizer_t;
	')

	webalizer_domtrans($1)
	role $2 types webalizer_t;
	allow webalizer_t $3:chr_file rw_term_perms;
')
