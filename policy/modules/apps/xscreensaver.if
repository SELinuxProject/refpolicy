## <summary>Modular screen saver and locker for X11.</summary>

########################################
## <summary>
##	Role access for xscreensaver.
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	User domain for the role.
##	</summary>
## </param>
#
interface(`xscreensaver_role',`
	gen_require(`
		attribute_role xscreensaver_roles;
		attribute_role xscreensaver_helper_roles;
		type xscreensaver_t, xscreensaver_exec_t;
		type xscreensaver_helper_t;
		type xscreensaver_config_t, xscreensaver_tmpfs_t;
	')

	roleattribute $1 xscreensaver_roles;
	roleattribute $1 xscreensaver_helper_roles;

	domtrans_pattern($2, xscreensaver_exec_t, xscreensaver_t)

	allow $2 xscreensaver_t:process { ptrace signal_perms };
	ps_process_pattern($2, xscreensaver_t)

	allow $2 xscreensaver_config_t:file { manage_file_perms relabel_file_perms };

	allow $2 xscreensaver_tmpfs_t:dir { manage_dir_perms relabel_dir_perms };
	allow $2 xscreensaver_tmpfs_t:file { manage_file_perms relabel_file_perms };

	allow xscreensaver_helper_t $2:fd use;
')
