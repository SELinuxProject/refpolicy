## <summary>Shoreline Firewall high-level tool for configuring netfilter.</summary>

########################################
## <summary>
##	Execute a domain transition to run shorewall.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`shorewall_domtrans',`
	gen_require(`
		type shorewall_t, shorewall_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, shorewall_exec_t, shorewall_t)
')

######################################
## <summary>
##	Execute a domain transition to run shorewall
##	using executables from /var/lib.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`shorewall_lib_domtrans',`
	gen_require(`
		type shorewall_t, shorewall_var_lib_t;
	')

	files_search_var_lib($1)
	domtrans_pattern($1, shorewall_var_lib_t, shorewall_t)
')

#######################################
## <summary>
##	Read shorewall configuration files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`shorewall_read_config',`
	gen_require(`
		type shorewall_etc_t;
	')

	files_search_etc($1)
	read_files_pattern($1, shorewall_etc_t, shorewall_etc_t)
')

######################################
## <summary>
##	Read shorewall lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`shorewall_read_lib_files',`
	gen_require(`
		type shorewall_var_lib_t;
	')

	files_search_var_lib($1)
	read_files_pattern($1, shorewall_var_lib_t, shorewall_var_lib_t)
')

#######################################
## <summary>
##	Read and write shorewall lib files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`shorewall_rw_lib_files',`
	gen_require(`
		type shorewall_var_lib_t;
	')

	files_search_var_lib($1)
	rw_files_pattern($1, shorewall_var_lib_t, shorewall_var_lib_t)
')

#######################################
## <summary>
##	Read shorewall temporary files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`shorewall_read_tmp_files',`
	gen_require(`
		type shorewall_tmp_t;
	')

	files_search_tmp($1)
	read_files_pattern($1, shorewall_tmp_t, shorewall_tmp_t)
')

#######################################
## <summary>
##	All of the rules required to
##	administrate an shorewall environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`shorewall_admin',`
	gen_require(`
		type shorewall_t, shorewall_lock_t, shorewall_log_t;
		type shorewall_exec_t, shorewall_initrc_exec_t, shorewall_var_lib_t;
		type shorewall_tmp_t, shorewall_etc_t;
	')

	allow $1 shorewall_t:process { ptrace signal_perms };
	ps_process_pattern($1, shorewall_t)

	init_startstop_service($1, $2, shorewall_t, shorewall_initrc_exec_t)

	can_exec($1, shorewall_exec_t)

	files_list_etc($1)
	admin_pattern($1, shorewall_etc_t)

	files_list_locks($1)
	admin_pattern($1, shorewall_lock_t)

	logging_list_logs($1)
	admin_pattern($1, shorewall_log_t)

	files_list_var_lib($1)
	admin_pattern($1, shorewall_var_lib_t)

	files_list_tmp($1)
	admin_pattern($1, shorewall_tmp_t)
')
