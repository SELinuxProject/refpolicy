## <summary>Automated backup program.</summary>

########################################
## <summary>
##	Execute amrecover in the amanda_recover domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
interface(`amanda_domtrans_recover',`
	gen_require(`
		type amanda_recover_t, amanda_recover_exec_t;
	')

	domain_auto_trans($1,amanda_recover_exec_t,amanda_recover_t)

	allow $1 amanda_recover_t:fd use;
	allow amanda_recover_t $1:fd use;
	allow amanda_recover_t $1:fifo_file rw_file_perms;
	allow amanda_recover_t $1:process sigchld;
')

########################################
## <summary>
##	Execute amrecover in the amanda_recover domain, and
##	allow the specified role the amanda_recover domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the amanda_recover domain.
##	</summary>
## </param>
## <param name="terminal">
##	<summary>
##	The type of the terminal allow the amanda_recover domain to use.
##	</summary>
## </param>
## <rolecap/>
#
interface(`amanda_run_recover',`
	gen_require(`
		type amanda_recover_t;
	')

	amanda_domtrans_recover($1)
	role $2 types amanda_recover_t;
	allow amanda_recover_t $3:chr_file rw_term_perms;
')

########################################
## <summary>
##	Search amanda library directories.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
interface(`amanda_search_lib',`
	gen_require(`
		type amanda_usr_lib_t;
	')

	allow $1 amanda_usr_lib_t:dir search;
	files_search_usr($1)
')

########################################
## <summary>
##	Do not audit attempts to read /etc/dumpdates.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`amanda_dontaudit_read_dumpdates',`
	gen_require(`
		type amanda_dumpdates_t;
	')

	dontaudit $1 amanda_dumpdates_t:file { getattr read };
')

########################################
## <summary>
##	Allow read/writing /etc/dumpdates.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to allow
##	</summary>
## </param>
#
interface(`amanda_rw_dumpdates_files',`
	gen_require(`
		type amanda_dumpdates_t;
	')

	allow $1 amanda_dumpdates_t:file rw_file_perms;
')
########################################
## <summary>
##	Allow read/writing amanda logs
## </summary>
## <param name="domain">
##	<summary>
##	Domain to allow
##	</summary>
## </param>
#
interface(`amanda_append_log_files',`
	gen_require(`
		type amanda_log_t;
	')

	allow $1 amanda_log_t:file ra_file_perms;
')


