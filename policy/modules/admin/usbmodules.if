## <summary>List kernel modules of USB devices</summary>

########################################
## <summary>
##	Execute usbmodules in the usbmodules domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`usbmodules_domtrans',`
	gen_require(`
		type usbmodules_t, usbmodules_exec_t;
	')

	domain_auto_trans($1, usbmodules_exec_t, usbmodules_t)

	allow $1 usbmodules_t:fd use;
	allow usbmodules_t $1:fd use;
	allow usbmodules_t $1:fifo_file rw_file_perms;
	allow usbmodules_t $1:process sigchld;

')

########################################
## <summary>
##	Execute usbmodules in the usbmodules domain, and
##	allow the specified role the usbmodules domain,
##	and use the caller's terminal.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the usbmodules domain.
##	</summary>
## </param>
## <param name="terminal">
##	<summary>
##	The type of the terminal allow the usbmodules domain to use.
##	</summary>
## </param>
## <rolecap/>
#
interface(`usbmodules_run',`
	gen_require(`
		type usbmodules_t;
	')

	usbmodules_domtrans($1)
	role $2 types usbmodules_t;
	allow usbmodules_t $3:chr_file rw_term_perms;
')
