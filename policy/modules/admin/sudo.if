## <summary>Execute a command with a substitute user</summary>

#######################################
## <summary>
##	The role template for the sudo module.
## </summary>
## <desc>
##	<p>
##	This template creates a derived domain which is allowed
##	to change the linux user id, to run commands as a different
##	user.
##	</p>
## </desc>
## <param name="role_prefix">
##	<summary>
##	The prefix of the user role (e.g., user
##	is the prefix for user_r).
##	</summary>
## </param>
## <param name="user_domain">
##	<summary>
##	User domain for the role.
##	</summary>
## </param>
## <param name="user_exec_domain">
##	<summary>
##	User exec domain for execute and transition access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access
##	</summary>
## </param>
#
template(`sudo_role_template',`

	gen_require(`
		type sudo_exec_t;
		attribute sudodomain;
	')

	##############################
	#
	# Declarations
	#

	type $1_sudo_t, sudodomain;
	userdom_user_application_domain($1_sudo_t, sudo_exec_t)
	domain_interactive_fd($1_sudo_t)
	domain_role_change_exemption($1_sudo_t)
	role $4 types $1_sudo_t;

	##############################
	#
	# Local Policy
	#

	# Use capabilities.
	allow $1_sudo_t self:capability { chown dac_override fowner kill setgid setuid sys_nice sys_resource };
	allow $1_sudo_t self:process { signal_perms getsched setsched getsession getpgid setpgid getcap setcap share getattr getrlimit rlimitinh siginh transition setsockcreate dyntransition noatsecure setkeycreate };
	allow $1_sudo_t self:process { setexec setrlimit };
	allow $1_sudo_t self:fd use;
	allow $1_sudo_t self:fifo_file rw_fifo_file_perms;
	allow $1_sudo_t self:shm create_shm_perms;
	allow $1_sudo_t self:sem create_sem_perms;
	allow $1_sudo_t self:msgq create_msgq_perms;
	allow $1_sudo_t self:msg { send receive };
	allow $1_sudo_t self:unix_dgram_socket create_socket_perms;
	allow $1_sudo_t self:unix_stream_socket create_stream_socket_perms;
	allow $1_sudo_t self:unix_dgram_socket sendto;
	allow $1_sudo_t self:unix_stream_socket connectto;
	allow $1_sudo_t self:key manage_key_perms;
	dontaudit $1_sudo_t self:capability { dac_read_search sys_ptrace };

        dontaudit $1_sudo_t $3:socket_class_set { read write };

	# By default, revert to the calling domain when a shell is executed.
	corecmd_shell_domtrans($1_sudo_t, $2)
	corecmd_bin_domtrans($1_sudo_t, $2)

	kernel_read_kernel_sysctls($1_sudo_t)
	kernel_read_system_state($1_sudo_t)
	kernel_link_key($1_sudo_t)
	kernel_dontaudit_getattr_proc($1_sudo_t)

	corecmd_exec_all_executables($1_sudo_t)

	dev_getattr_fs($1_sudo_t)
	dev_read_urand($1_sudo_t)
	dev_rw_generic_usb_dev($1_sudo_t)
	dev_read_sysfs($1_sudo_t)

	domain_use_interactive_fds($1_sudo_t)
	domain_sigchld_interactive_fds($1_sudo_t)
	domain_getattr_all_entry_files($1_sudo_t)

	files_read_etc_files($1_sudo_t)
	files_read_var_files($1_sudo_t)
	files_read_usr_symlinks($1_sudo_t)
	files_getattr_usr_files($1_sudo_t)
	# for some PAM modules and for cwd
	files_dontaudit_search_home($1_sudo_t)
	files_list_tmp($1_sudo_t)

	fs_search_auto_mountpoints($1_sudo_t)
	fs_getattr_xattr_fs($1_sudo_t)

	selinux_validate_context($1_sudo_t)
	selinux_compute_relabel_context($1_sudo_t)

	term_getattr_pty_fs($1_sudo_t)
	term_dontaudit_getattr_unallocated_ttys($1_sudo_t)
	term_relabel_all_ttys($1_sudo_t)
	term_relabel_all_ptys($1_sudo_t)

	auth_run_chk_passwd($1_sudo_t, $4)
	# sudo stores a token in the pam runtime directory
	auth_manage_pam_runtime_dirs($1_sudo_t)
	auth_manage_pam_runtime_files($1_sudo_t)
	auth_use_pam($1_sudo_t)
	auth_runtime_filetrans_pam_runtime($1_sudo_t, dir, "sudo")

	init_rw_utmp($1_sudo_t)

	logging_send_audit_msgs($1_sudo_t)
	logging_send_syslog_msg($1_sudo_t)

	miscfiles_read_localization($1_sudo_t)

	seutil_read_default_contexts($1_sudo_t)
	seutil_libselinux_linked($1_sudo_t)

	userdom_spec_domtrans_all_users($1_sudo_t)
	userdom_create_all_users_keys($1_sudo_t)
	userdom_create_user_pty($1_sudo_t)
	userdom_manage_user_home_content_files($1_sudo_t)
	userdom_manage_user_home_content_symlinks($1_sudo_t)
	userdom_manage_user_tmp_files($1_sudo_t)
	userdom_manage_user_tmp_symlinks($1_sudo_t)
	userdom_setattr_user_ptys($1_sudo_t)
	userdom_use_user_terminals($1_sudo_t)
	userdom_dontaudit_rw_user_tmp_pipes($1_sudo_t)
	# for some PAM modules and for cwd
	userdom_dontaudit_search_user_home_content($1_sudo_t)
	userdom_dontaudit_search_user_home_dirs($1_sudo_t)

	tunable_policy(`allow_polyinstantiation',`
		allow $1_sudo_t self:capability sys_admin;
		fs_mount_xattr_fs($1_sudo_t)
		fs_unmount_xattr_fs($1_sudo_t)
	')

	tunable_policy(`sudo_allow_user_exec_domains',`
		allow $1_sudo_t $3:key search;

		# Transmit SIGWINCH to children
		allow $1_sudo_t $3:process signal;

		# Enter this derived domain from the user domain
		domtrans_pattern($3, sudo_exec_t, $1_sudo_t)

		allow $3 $1_sudo_t:fd use;
		allow $3 $1_sudo_t:fifo_file rw_fifo_file_perms;
		allow $3 $1_sudo_t:process signal_perms;
	',`
		allow $1_sudo_t $2:key search;

		# Transmit SIGWINCH to children
		allow $1_sudo_t $2:process signal;

		# Enter this derived domain from the user domain
		domtrans_pattern($2, sudo_exec_t, $1_sudo_t)

		allow $2 $1_sudo_t:fd use;
		allow $2 $1_sudo_t:fifo_file rw_fifo_file_perms;
		allow $2 $1_sudo_t:process signal_perms;
	')

	tunable_policy(`use_nfs_home_dirs',`
		fs_manage_nfs_files($1_sudo_t)
	')

	tunable_policy(`use_samba_home_dirs',`
		fs_manage_cifs_files($1_sudo_t)
	')

	optional_policy(`
		dbus_system_bus_client($1_sudo_t)

		ifdef(`init_systemd',`
			init_dbus_chat($1_sudo_t)
		')
	')

	optional_policy(`
		fprintd_dbus_chat($1_sudo_t)
	')

')

########################################
## <summary>
##	Send a SIGCHLD signal to the sudo domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`sudo_sigchld',`
	gen_require(`
		attribute sudodomain;
	')

	allow $1 sudodomain:process sigchld;
')
