## <summary>Daemon used by MiniUPnPc to speed up device discoveries.</summary>

########################################
## <summary>
##	Read minissdpd configuration files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`minissdpd_read_config',`
	gen_require(`
		type minissdpd_conf_t;
	')

	files_search_etc($1)
	allow $1 minissdpd_conf_t:file read_file_perms;
')

########################################
## <summary>
##	All of the rules required to
##	administrate an minissdpd environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`minissdpd_admin',`
	gen_require(`
		type minissdpd_t, minissdpd_initrc_exec_t, minissdpd_conf_t;
		type minissdpd_runtime_t;
	')

	allow $1 minissdpd_t:process { ptrace signal_perms };
	ps_process_pattern($1, minissdpd_t)

	init_startstop_service($1, $2, minissdpd_t, minissdpd_initrc_exec_t)

	files_search_etc($1)
	admin_pattern($1, minissdpd_conf_t)

	files_search_pids($1)
	admin_pattern($1, minissdpd_runtime_t)
')
