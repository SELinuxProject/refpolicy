## <summary>Automatic IPv6 Connectivity Client Utility.</summary>

########################################
## <summary>
##	Execute a domain transition to run aiccu.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`aiccu_domtrans',`
	gen_require(`
		type aiccu_t, aiccu_exec_t;
	')

	domtrans_pattern($1, aiccu_exec_t, aiccu_t)
	corecmd_search_bin($1)
')

########################################
## <summary>
##	Execute aiccu server in the aiccu domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`aiccu_initrc_domtrans',`
	gen_require(`
		type aiccu_initrc_exec_t;
	')

	init_labeled_script_domtrans($1, aiccu_initrc_exec_t)
')

########################################
## <summary>
##	Read aiccu PID files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`aiccu_read_pid_files',`
	gen_require(`
		type aiccu_runtime_t;
	')

	allow $1 aiccu_runtime_t:file read_file_perms;
	files_search_pids($1)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an aiccu environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`aiccu_admin',`
	gen_require(`
		type aiccu_t, aiccu_initrc_exec_t, aiccu_etc_t;
		type aiccu_runtime_t;
	')

	allow $1 aiccu_t:process { ptrace signal_perms };
	ps_process_pattern($1, aiccu_t)

	init_startstop_service($1, $2, aiccu_t, aiccu_initrc_exec_t)

	admin_pattern($1, aiccu_etc_t)
	files_list_etc($1)

	admin_pattern($1, aiccu_runtime_t)
	files_list_pids($1)
')
