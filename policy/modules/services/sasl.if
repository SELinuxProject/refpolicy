## <summary>SASL authentication server.</summary>

########################################
## <summary>
##	Connect to SASL.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`sasl_connect',`
	gen_require(`
		type saslauthd_t, saslauthd_runtime_t;
	')

	files_search_runtime($1)
	stream_connect_pattern($1, saslauthd_runtime_t, saslauthd_runtime_t, saslauthd_t)
')

########################################
## <summary>
##	Read SASL keytab files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`sasl_read_keytab',`
	gen_require(`
		type saslauthd_keytab_t;
	')

	files_search_etc($1)
	read_files_pattern($1, saslauthd_keytab_t, saslauthd_keytab_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an sasl environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`sasl_admin',`
	gen_require(`
		type saslauthd_t, saslauthd_runtime_t, saslauthd_initrc_exec_t;
		type saslauthd_keytab_t;
	')

	allow $1 saslauthd_t:process { ptrace signal_perms };
	ps_process_pattern($1, saslauthd_t)

	init_startstop_service($1, $2, saslauthd_t, saslauthd_initrc_exec_t)

	files_list_etc($1)
	admin_pattern($1, saslauthd_keytab_t)

	files_list_runtime($1)
	admin_pattern($1, saslauthd_runtime_t)
')
