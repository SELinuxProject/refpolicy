## <summary>X Windows Font Server.</summary>

########################################
## <summary>
##	Read xfs temporary sock files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`xfs_read_sockets',`
	gen_require(`
		type xfs_tmp_t;
	')

	files_search_tmp($1)
	read_sock_files_pattern($1, xfs_tmp_t, xfs_tmp_t)
')

########################################
## <summary>
##	Connect to xfs with a unix
##	domain stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`xfs_stream_connect',`
	gen_require(`
		type xfs_tmp_t, xfs_t;
	')

	files_search_tmp($1)
	stream_connect_pattern($1, xfs_tmp_t, xfs_tmp_t, xfs_t)
')

########################################
## <summary>
##	Execute xfs in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`xfs_exec',`
	gen_require(`
		type xfs_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, xfs_exec_t)
')

########################################
## <summary>
##	Create xfs temporary dirs
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`xfs_create_tmp_dirs',`
	gen_require(`
		type xfs_tmp_t;
	')

	files_search_tmp($1)
	allow $1 xfs_tmp_t:dir create;
')

########################################
## <summary>
##	All of the rules required to
##	administrate an xfs environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`xfs_admin',`
	gen_require(`
		type xfs_t, xfs_initrc_exec_t, xfs_runtime_t;
		type xfs_tmp_t;
	')

	allow $1 xfs_t:process { ptrace signal_perms };
	ps_process_pattern($1, xfs_t)

	init_startstop_service($1, $2, xfs_t, xfs_initrc_exec_t)

	files_search_pids($1)
	admin_pattern($1, xfs_runtime_t)

	files_search_tmp($1)
	admin_pattern($1, xfs_tmp_t)
')
