## <summary>The onion router.</summary>

########################################
## <summary>
##	Execute a domain transition to run tor.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`tor_domtrans',`
	gen_require(`
		type tor_t, tor_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, tor_exec_t, tor_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an tor environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`tor_admin',`
	gen_require(`
		type tor_t, tor_var_log_t, tor_etc_t;
		type tor_var_lib_t, tor_runtime_t, tor_initrc_exec_t;
	')

	allow $1 tor_t:process { ptrace signal_perms };
	ps_process_pattern($1, tor_t)

	init_startstop_service($1, $2, tor_t, tor_initrc_exec_t)

	files_list_etc($1)
	admin_pattern($1, tor_etc_t)

	files_list_var_lib($1)
	admin_pattern($1, tor_var_lib_t)

	logging_list_logs($1)
	admin_pattern($1, tor_var_log_t)

	files_list_runtime($1)
	admin_pattern($1, tor_runtime_t)
')

########################################
## <summary>
##      Read and write Tor pluggable transport state var files.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed access.
##      </summary>
## </param>
#
interface(`tor_rw_pt_state_var_files',`
        gen_require(`
                type tor_var_lib_t;
                type tor_pt_state_var_lib_t;
        ')

        files_search_var_lib($1)
        allow $1 tor_var_lib_t:dir search_dir_perms;
        allow $1 tor_pt_state_var_lib_t:dir search_dir_perms;
        rw_files_pattern($1, tor_var_lib_t, tor_pt_state_var_lib_t)
')
