## <summary>Ricci cluster management agent.</summary>

########################################
## <summary>
##	Execute a domain transition to run ricci.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`ricci_domtrans',`
	gen_require(`
		type ricci_t, ricci_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, ricci_exec_t, ricci_t)
')

########################################
## <summary>
##	Execute a domain transition to
##	run ricci modcluster.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`ricci_domtrans_modcluster',`
	gen_require(`
		type ricci_modcluster_t, ricci_modcluster_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, ricci_modcluster_exec_t, ricci_modcluster_t)
')

########################################
## <summary>
##	Do not audit attempts to use
##	ricci modcluster file descriptors.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`ricci_dontaudit_use_modcluster_fds',`
	gen_require(`
		type ricci_modcluster_t;
	')

	dontaudit $1 ricci_modcluster_t:fd use;
')

########################################
## <summary>
##	Do not audit attempts to read write
##	ricci modcluster unamed pipes.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`ricci_dontaudit_rw_modcluster_pipes',`
	gen_require(`
		type ricci_modcluster_t;
	')

	dontaudit $1 ricci_modcluster_t:fifo_file { read write };
')

########################################
## <summary>
##	Connect to ricci_modclusterd with
##	a unix domain stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`ricci_stream_connect_modclusterd',`
	gen_require(`
		type ricci_modclusterd_t, ricci_modcluster_runtime_t;
	')

	files_search_pids($1)
	stream_connect_pattern($1, ricci_modcluster_runtime_t, ricci_modcluster_runtime_t, ricci_modclusterd_t)
')

########################################
## <summary>
##	Execute a domain transition to
##	run ricci modlog.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`ricci_domtrans_modlog',`
	gen_require(`
		type ricci_modlog_t, ricci_modlog_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, ricci_modlog_exec_t, ricci_modlog_t)
')

########################################
## <summary>
##	Execute a domain transition to
##	run ricci modrpm.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`ricci_domtrans_modrpm',`
	gen_require(`
		type ricci_modrpm_t, ricci_modrpm_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, ricci_modrpm_exec_t, ricci_modrpm_t)
')

########################################
## <summary>
##	Execute a domain transition to
##	run ricci modservice.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`ricci_domtrans_modservice',`
	gen_require(`
		type ricci_modservice_t, ricci_modservice_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, ricci_modservice_exec_t, ricci_modservice_t)
')

########################################
## <summary>
##	Execute a domain transition to
##	run ricci modstorage.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`ricci_domtrans_modstorage',`
	gen_require(`
		type ricci_modstorage_t, ricci_modstorage_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, ricci_modstorage_exec_t, ricci_modstorage_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an ricci environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`ricci_admin',`
	gen_require(`
		type ricci_t, ricci_initrc_exec_t, ricci_tmp_t;
		type ricci_var_lib_t, ricci_var_log_t, ricci_runtime_t;
	')

	allow $1 ricci_t:process { ptrace signal_perms };
	ps_process_pattern($1, ricci_t)

	init_startstop_service($1, $2, ricci_t, ricci_initrc_exec_t)

	files_list_tmp($1)
	admin_pattern($1, ricci_tmp_t)

	files_list_var_lib($1)
	admin_pattern($1, ricci_var_lib_t)

	logging_list_logs($1)
	admin_pattern($1, ricci_var_log_t)

	files_list_pids($1)
	admin_pattern($1, ricci_runtime_t)
')
