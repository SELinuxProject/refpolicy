
policy_module(arpwatch,1.2.0)

########################################
#
# Declarations
#

type arpwatch_t;
type arpwatch_exec_t;
init_daemon_domain(arpwatch_t,arpwatch_exec_t)

type arpwatch_data_t;
files_type(arpwatch_data_t)

type arpwatch_tmp_t;
files_tmp_file(arpwatch_tmp_t)

type arpwatch_var_run_t;
files_pid_file(arpwatch_var_run_t)

########################################
#
# Local policy
#
allow arpwatch_t self:capability { net_admin net_raw setgid setuid };
dontaudit arpwatch_t self:capability sys_tty_config;
allow arpwatch_t self:process signal_perms;
allow arpwatch_t self:unix_dgram_socket create_socket_perms;
allow arpwatch_t self:unix_stream_socket create_stream_socket_perms;
allow arpwatch_t self:netlink_route_socket r_netlink_socket_perms;
allow arpwatch_t self:tcp_socket { connect create_stream_socket_perms };
allow arpwatch_t self:udp_socket create_socket_perms;
allow arpwatch_t self:packet_socket create_socket_perms;

allow arpwatch_t arpwatch_data_t:dir create_dir_perms;
allow arpwatch_t arpwatch_data_t:file create_file_perms;
allow arpwatch_t arpwatch_data_t:lnk_file create_lnk_perms;

allow arpwatch_t arpwatch_tmp_t:dir create_dir_perms;
allow arpwatch_t arpwatch_tmp_t:file create_file_perms;
files_tmp_filetrans(arpwatch_t, arpwatch_tmp_t, { file dir })

allow arpwatch_t arpwatch_var_run_t:file create_file_perms;
allow arpwatch_t arpwatch_var_run_t:dir rw_dir_perms;
files_pid_filetrans(arpwatch_t,arpwatch_var_run_t,file)

kernel_read_kernel_sysctls(arpwatch_t)
kernel_list_proc(arpwatch_t)
kernel_read_proc_symlinks(arpwatch_t)

corenet_non_ipsec_sendrecv(arpwatch_t)
corenet_tcp_sendrecv_all_if(arpwatch_t)
corenet_udp_sendrecv_all_if(arpwatch_t)
corenet_raw_sendrecv_all_if(arpwatch_t)
corenet_tcp_sendrecv_all_nodes(arpwatch_t)
corenet_udp_sendrecv_all_nodes(arpwatch_t)
corenet_raw_sendrecv_all_nodes(arpwatch_t)
corenet_tcp_sendrecv_all_ports(arpwatch_t)
corenet_udp_sendrecv_all_ports(arpwatch_t)

dev_read_sysfs(arpwatch_t)

fs_getattr_all_fs(arpwatch_t)
fs_search_auto_mountpoints(arpwatch_t)

term_dontaudit_use_console(arpwatch_t)

corecmd_read_sbin_symlinks(arpwatch_t)

domain_use_interactive_fds(arpwatch_t)

files_read_etc_files(arpwatch_t)
files_read_usr_files(arpwatch_t)
files_search_var_lib(arpwatch_t)

init_use_fds(arpwatch_t)
init_use_script_ptys(arpwatch_t)

libs_use_ld_so(arpwatch_t)
libs_use_shared_libs(arpwatch_t)

logging_send_syslog_msg(arpwatch_t)

miscfiles_read_localization(arpwatch_t)

sysnet_read_config(arpwatch_t)

userdom_dontaudit_use_unpriv_user_fds(arpwatch_t)
userdom_dontaudit_search_sysadm_home_dirs(arpwatch_t)

mta_send_mail(arpwatch_t)

ifdef(`targeted_policy',`
	term_dontaudit_use_unallocated_ttys(arpwatch_t)
	term_dontaudit_use_generic_ptys(arpwatch_t)
	files_dontaudit_read_root_files(arpwatch_t)
')

optional_policy(`
	nis_use_ypbind(arpwatch_t)
')

optional_policy(`
	corecmd_search_bin(arpwatch_t)
')

optional_policy(`
	seutil_sigchld_newrole(arpwatch_t)
')

optional_policy(`
	udev_read_db(arpwatch_t)
')

