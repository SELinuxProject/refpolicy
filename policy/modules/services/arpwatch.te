
policy_module(arpwatch, 1.8.0)

########################################
#
# Declarations
#

type arpwatch_t;
type arpwatch_exec_t;
init_daemon_domain(arpwatch_t, arpwatch_exec_t)

type arpwatch_data_t;
files_type(arpwatch_data_t)

type arpwatch_initrc_exec_t;
init_script_file(arpwatch_initrc_exec_t)

type arpwatch_tmp_t;
files_tmp_file(arpwatch_tmp_t)

type arpwatch_var_run_t;
files_pid_file(arpwatch_var_run_t)

########################################
#
# Local policy
#
allow arpwatch_t self:capability { net_admin net_raw setgid setuid };
dontaudit arpwatch_t self:capability sys_tty_config;
allow arpwatch_t self:process signal_perms;
allow arpwatch_t self:unix_dgram_socket create_socket_perms;
allow arpwatch_t self:unix_stream_socket create_stream_socket_perms;
allow arpwatch_t self:tcp_socket { connect create_stream_socket_perms };
allow arpwatch_t self:udp_socket create_socket_perms;
allow arpwatch_t self:packet_socket create_socket_perms;

manage_dirs_pattern(arpwatch_t, arpwatch_data_t, arpwatch_data_t)
manage_files_pattern(arpwatch_t, arpwatch_data_t, arpwatch_data_t)
manage_lnk_files_pattern(arpwatch_t, arpwatch_data_t, arpwatch_data_t)

manage_dirs_pattern(arpwatch_t, arpwatch_tmp_t, arpwatch_tmp_t)
manage_files_pattern(arpwatch_t, arpwatch_tmp_t, arpwatch_tmp_t)
files_tmp_filetrans(arpwatch_t, arpwatch_tmp_t, { file dir })

manage_files_pattern(arpwatch_t, arpwatch_var_run_t, arpwatch_var_run_t)
files_pid_filetrans(arpwatch_t, arpwatch_var_run_t, file)

kernel_read_kernel_sysctls(arpwatch_t)
kernel_list_proc(arpwatch_t)
kernel_read_proc_symlinks(arpwatch_t)

corenet_all_recvfrom_unlabeled(arpwatch_t)
corenet_all_recvfrom_netlabel(arpwatch_t)
corenet_tcp_sendrecv_generic_if(arpwatch_t)
corenet_udp_sendrecv_generic_if(arpwatch_t)
corenet_raw_sendrecv_generic_if(arpwatch_t)
corenet_tcp_sendrecv_generic_node(arpwatch_t)
corenet_udp_sendrecv_generic_node(arpwatch_t)
corenet_raw_sendrecv_generic_node(arpwatch_t)
corenet_tcp_sendrecv_all_ports(arpwatch_t)
corenet_udp_sendrecv_all_ports(arpwatch_t)

dev_read_sysfs(arpwatch_t)

fs_getattr_all_fs(arpwatch_t)
fs_search_auto_mountpoints(arpwatch_t)

corecmd_read_bin_symlinks(arpwatch_t)

domain_use_interactive_fds(arpwatch_t)

files_read_etc_files(arpwatch_t)
files_read_usr_files(arpwatch_t)
files_search_var_lib(arpwatch_t)

auth_use_nsswitch(arpwatch_t)

logging_send_syslog_msg(arpwatch_t)

miscfiles_read_localization(arpwatch_t)

userdom_dontaudit_search_user_home_dirs(arpwatch_t)
userdom_dontaudit_use_unpriv_user_fds(arpwatch_t)

mta_send_mail(arpwatch_t)

optional_policy(`
	seutil_sigchld_newrole(arpwatch_t)
')

optional_policy(`
	udev_read_db(arpwatch_t)
')
