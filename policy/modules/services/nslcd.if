## <summary>Local LDAP name service daemon.</summary>

########################################
## <summary>
##	Execute a domain transition to run nslcd.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`nslcd_domtrans',`
	gen_require(`
		type nslcd_t, nslcd_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, nslcd_exec_t, nslcd_t)
')

########################################
## <summary>
##	Execute nslcd server in the nslcd domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`nslcd_initrc_domtrans',`
	gen_require(`
		type nslcd_initrc_exec_t;
	')

	init_labeled_script_domtrans($1, nslcd_initrc_exec_t)
')

########################################
## <summary>
##	Read nslcd pid files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nslcd_read_pid_files',`
	gen_require(`
		type nslcd_runtime_t;
	')

	files_search_pids($1)
	allow $1 nslcd_runtime_t:file read_file_perms;
')

########################################
## <summary>
##	Connect to nslcd over an unix
##	domain stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`nslcd_stream_connect',`
	gen_require(`
		type nslcd_t, nslcd_runtime_t;
	')

	files_search_pids($1)
	stream_connect_pattern($1, nslcd_runtime_t, nslcd_runtime_t, nslcd_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an nslcd environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`nslcd_admin',`
	gen_require(`
		type nslcd_t, nslcd_initrc_exec_t, nslcd_runtime_t;
		type nslcd_conf_t;
	')

	allow $1 nslcd_t:process { ptrace signal_perms };
	ps_process_pattern($1, nslcd_t)

	init_startstop_service($1, $2, nslcd_t, nslcd_initrc_exec_t)

	files_search_etc($1)
	admin_pattern($1, nslcd_conf_t)

	files_search_pids($1)
	admin_pattern($1, nslcd_runtime_t)
')
