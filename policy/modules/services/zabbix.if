## <summary>Distributed infrastructure monitoring.</summary>

########################################
## <summary>
##	Execute a domain transition to run zabbix.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`zabbix_domtrans',`
	gen_require(`
		type zabbix_t, zabbix_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, zabbix_exec_t, zabbix_t)
')

########################################
## <summary>
##	Connect to zabbit on the TCP network.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`zabbix_tcp_connect',`
	gen_require(`
		type zabbix_t;
	')

	corenet_sendrecv_zabbix_client_packets($1)
	corenet_tcp_connect_zabbix_port($1)
	corenet_tcp_recvfrom_labeled($1, zabbix_t)
')

########################################
## <summary>
##	Read zabbix log files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`zabbix_read_log',`
	gen_require(`
		type zabbix_log_t;
	')

	logging_search_logs($1)
	read_files_pattern($1, zabbix_log_t, zabbix_log_t)
')

########################################
## <summary>
##	Append zabbix log files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`zabbix_append_log',`
	gen_require(`
		type zabbix_log_t;
	')

	logging_search_logs($1)
	append_files_pattern($1, zabbix_log_t, zabbix_log_t)
')

########################################
## <summary>
##	Connect to zabbix agent on the TCP network.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`zabbix_agent_tcp_connect',`
	gen_require(`
		type zabbix_t;
	')

	corenet_sendrecv_zabbix_agent_client_packets($1)
	corenet_tcp_connect_zabbix_agent_port($1)
	corenet_tcp_recvfrom_labeled($1, zabbix_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an zabbix environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`zabbix_admin',`
	gen_require(`
		type zabbix_t, zabbix_agent_t, zabbix_log_t, zabbix_runtime_t;
		type zabbix_initrc_exec_t, zabbix_agent_initrc_exec_t, zabbix_tmp_t;
		type zabbix_tmpfs_t;
	')

	allow $1 { zabbix_t zabbix_agent_t }:process { ptrace signal_perms };
	ps_process_pattern($1, { zabbix_t zabbix_agent_t })

	init_startstop_service($1, $2, zabbix_t, zabbix_initrc_exec_t)
	init_startstop_service($1, $2, zabbix_agent_t, zabbix_agent_initrc_exec_t)

	logging_list_logs($1)
	admin_pattern($1, zabbix_log_t)

	files_list_runtime($1)
	admin_pattern($1, zabbix_runtime_t)

	files_list_tmp($1)
	admin_pattern($1, zabbix_tmp_t)

	fs_list_tmpfs($1)
	admin_pattern($1, zabbix_tmpfs_t)
')
