## <summary>PCMCIA card management services</summary>

########################################
## <summary>
##	Execute cardctl in the cardmgr domain.
## </summary>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`pcmcia_domtrans_cardctl',`
	gen_require(`
		type cardmgr_t, cardctl_exec_t;
		class process sigchld;
		class fd use;
		class fifo_file rw_file_perms;
	')

	domain_auto_trans($1,cardctl_exec_t,cardmgr_t)

	allow $1 cardmgr_t:fd use;
	allow cardmgr_t $1:fd use;
	allow cardmgr_t $1:fifo_file rw_file_perms;
	allow cardmgr_t $1:process sigchld;
')

########################################
## <summary>
##	Execute cardmgr in the cardctl domain, and
##	allow the specified role the cardmgr domain.
## </summary>
## <param name="domain">
##	The type of the process performing this action.
## </param>
## <param name="role">
##	The role to be allowed the cardmgr domain.
## </param>
## <param name="terminal">
##	The type of the terminal allow the cardmgr domain to use.
## </param>
#
interface(`pcmcia_run_cardctl',`
	gen_require(`
		type cardmgr_t;
		class chr_file rw_term_perms;
	')

	pcmcia_domtrans_cardctl($1)
	role $2 types cardmgr_t;
	allow cardmgr_t $3:chr_file rw_term_perms;
')
