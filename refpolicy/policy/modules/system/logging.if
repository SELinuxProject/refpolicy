## <summary>Policy for the kernel message logger and system logging daemon.</summary>

#######################################
## <summary>
##	Make the specified type a file
##	used for logs.
## </summary>
## <param name="file_type">
##	<summary>
##	Type of the file to be used as a log.
##	</summary>
## </param>
#
interface(`logging_log_file',`
	gen_require(`
		attribute logfile;
	')

	files_type($1)
	files_associate_tmp($1)
	fs_associate_tmpfs($1)
	typeattribute $1 logfile;
')

########################################
## <summary>
##	Read the audit log.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`logging_read_audit_log',`
	gen_require(`
		type auditd_log_t;
	')

	files_search_var($1)
	allow $1 auditd_log_t:dir r_dir_perms;
	allow $1 auditd_log_t:file r_file_perms;
')

########################################
## <summary>
##	Execute auditctl in the auditctl domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`logging_domtrans_auditctl',`
	gen_require(`
		type auditctl_t, auditctl_exec_t;
	')

	domain_auto_trans($1,auditctl_exec_t,auditctl_t)

	allow $1 auditctl_t:fd use;
	allow auditctl_t $1:fd use;
	allow auditctl_t $1:fifo_file rw_file_perms;
	allow auditctl_t $1:process sigchld;
')

########################################
## <summary>
##	Execute auditctl in the auditctl domain, and
##	allow the specified role the auditctl domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the auditctl domain.
##	</summary>
## </param>
## <param name="terminal">
##	<summary>
##	The type of the terminal allow the auditctl domain to use.
##	</summary>
## </param>
#
interface(`logging_run_auditctl',`
	gen_require(`
		type auditctl_t;
	')

	logging_domtrans_auditctl($1)
	role $2 types auditctl_t;
	allow auditctl_t $3:chr_file rw_term_perms;
')

########################################
## <summary>
##	Execute syslogd in the syslog domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
interface(`logging_domtrans_syslog',`
	gen_require(`
		type syslogd_t, syslogd_exec_t;
	')

	corecmd_search_sbin($1)
	domain_auto_trans($1,syslogd_exec_t,syslogd_t)

	allow $1 syslogd_t:fd use;
	allow syslogd_t $1:fd use;
	allow syslogd_t $1:fifo_file rw_file_perms;
	allow syslogd_t $1:process sigchld;
')

########################################
#
# logging_log_filetrans(domain,privatetype,[class(es)])
#
interface(`logging_log_filetrans',`
	gen_require(`
		type var_log_t;
	')

	files_search_var($1)
	allow $1 var_log_t:dir rw_dir_perms;
	type_transition $1 var_log_t:$3 $2;
')

#######################################
#
# logging_send_syslog_msg(domain)
#
interface(`logging_send_syslog_msg',`
	gen_require(`
		type syslogd_t, devlog_t;
	')

	allow $1 devlog_t:lnk_file read;
	allow $1 devlog_t:sock_file rw_file_perms;

	# the type of socket depends on the syslog daemon
	allow $1 syslogd_t:unix_dgram_socket sendto;
	allow $1 syslogd_t:unix_stream_socket connectto;
	allow $1 self:unix_dgram_socket create_socket_perms;
	allow $1 self:unix_stream_socket create_socket_perms;

	# cjp: this should most likely be removed:
	term_use_console($1)
')

########################################
## <summary>
##	Read the auditd configuration files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`logging_read_audit_config',`
	gen_require(`
		type auditd_etc_t;
	')

	files_search_etc($1)
	allow $1 auditd_etc_t:file r_file_perms;
')

########################################
## <summary>
##	Allows the domain to open a file in the
##	log directory, but does not allow the listing
##	of the contents of the log directory.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
interface(`logging_search_logs',`
	gen_require(`
		type var_log_t;
	')

	files_search_var($1)
	allow $1 var_log_t:dir search;
')

#######################################
## <summary>
##      Do not audit attempts to search the var log directory.
## </summary>
## <param name="domain">
##      <summary>
##      Domain not to audit.
##      </summary>
## </param>
#
interface(`logging_dontaudit_search_logs',`
	gen_require(`
		type var_log_t;
	')

	dontaudit $1 var_log_t:dir search;
')

#######################################
## <summary>
##	List the contents of the generic log directory (/var/log).
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`logging_list_logs',`
	gen_require(`
		type var_log_t;
	')

	files_search_var($1)
	allow $1 var_log_t:dir r_dir_perms;
')

#######################################
## <summary>
##	Read and write the generic log directory (/var/log).
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
interface(`logging_rw_generic_log_dirs',`
	gen_require(`
		type var_log_t;
	')

	files_search_var($1)
	allow $1 var_log_t:dir rw_dir_perms;
')

#######################################
#
# logging_dontaudit_getattr_all_logs(domain)
#
interface(`logging_dontaudit_getattr_all_logs',`
	gen_require(`
		attribute logfile;
	')

	dontaudit $1 logfile:file getattr;
')

#######################################
#
# logging_append_all_logs(domain)
#
interface(`logging_append_all_logs',`
	gen_require(`
		attribute logfile;
		type var_log_t;
	')

	files_search_var($1)
	allow $1 var_log_t:dir r_dir_perms;
	allow $1 logfile:file { getattr append };
')

#######################################
#
# logging_read_all_logs(domain)
#
interface(`logging_read_all_logs',`
	gen_require(`
		attribute logfile;
		type var_log_t;
	')

	files_search_var($1)
	allow $1 var_log_t:dir r_dir_perms;
	allow $1 logfile:file r_file_perms;
')

########################################
## <summary>
##	Execute all log files in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
# cjp: not sure why this is needed.  This was added
# because of logrotate.
interface(`logging_exec_all_logs',`
	gen_require(`
		attribute logfile;
	')

	files_search_var($1)
	allow $1 logfile:dir r_dir_perms;
	can_exec($1,logfile)
')

#######################################
#
# logging_manage_all_logs(domain)
#
interface(`logging_manage_all_logs',`
	gen_require(`
		attribute logfile;
	')

	files_search_var($1)
	allow $1 logfile:dir rw_dir_perms;
	allow $1 logfile:lnk_file read;
	allow $1 logfile:file create_file_perms;
')

#######################################
#
# logging_read_generic_logs(domain)
#
interface(`logging_read_generic_logs',`
	gen_require(`
		type var_log_t;
	')

	files_search_var($1)
	allow $1 var_log_t:dir r_dir_perms;
	allow $1 var_log_t:file r_file_perms;
')

#######################################
#
# logging_write_generic_logs(domain)
#
interface(`logging_write_generic_logs',`
	gen_require(`
		type var_log_t;
	')

	files_search_var($1)
	allow $1 var_log_t:dir r_dir_perms;
	allow $1 var_log_t:file { getattr write };
')

########################################
## <summary>
##	Read and write generic log files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`logging_rw_generic_logs',`
	gen_require(`
		type var_log_t;
	')

	files_search_var($1)
	allow $1 var_log_t:dir r_dir_perms;
	allow $1 var_log_t:file rw_file_perms;
')

########################################
## <summary>
##	Create, read, write, and delete
##	generic log files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`logging_manage_generic_logs',`
	gen_require(`
		type var_log_t;
	')

	files_search_var($1)
	allow $1 var_log_t:dir rw_dir_perms;
	allow $1 var_log_t:file create_file_perms;
')
