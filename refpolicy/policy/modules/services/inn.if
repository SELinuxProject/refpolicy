## <summary>Internet News NNTP server</summary>

########################################
## <summary>
##	Allow the specified domain to execute innd
##	in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`inn_exec',`
	gen_require(`
		type innd_t;
	')

	can_exec($1,innd_exec_t)
')

########################################
## <summary>
##	Allow the specified domain to execute
##	inn configuration files in /etc.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`inn_exec_config',`
	gen_require(`
		type innd_etc_t;
	')

	can_exec($1,innd_etc_t)
')

########################################
## <summary>
##	Create, read, write, and delete the innd log.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`inn_manage_log',`
	gen_require(`
		type innd_log_t;
	')

	logging_rw_generic_log_dirs($1)
	allow $1 innd_log_t:dir search;
	allow $1 innd_log_t:file create_file_perms;
')

########################################
## <summary>
##	Create, read, write, and delete the innd pid files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`inn_manage_pid',`
	gen_require(`
		type innd_var_run_t;
	')

	files_search_pids($1)
	allow $1 innd_var_run_t:dir rw_dir_perms;
	allow $1 innd_var_run_t:file create_file_perms;
	allow $1 innd_var_run_t:lnk_file create_lnk_perms;
')

########################################
## <summary>
##	Read innd configuration files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`inn_read_config',`
	gen_require(`
		type innd_etc_t;
	')

	allow $1 innd_etc_t:dir { getattr read search };
	allow $1 innd_etc_t:file { read getattr };
	allow $1 innd_etc_t:lnk_file { getattr read };
')

########################################
## <summary>
##	Read innd news library files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`inn_read_news_lib',`
	gen_require(`
		type innd_var_lib_t;
	')

	allow $1 innd_var_lib_t:dir { getattr read search };
	allow $1 innd_var_lib_t:file { read getattr };
	allow $1 innd_var_lib_t:lnk_file { getattr read };
')

########################################
## <summary>
##	Read innd news library files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`inn_read_news_spool',`
	gen_require(`
		type news_spool_t;
	')

	allow $1 news_spool_t:dir { getattr read search };
	allow $1 news_spool_t:file { read getattr };
	allow $1 news_spool_t:lnk_file { getattr read };
')

########################################
## <summary>
##	Send to a innd unix dgram socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`inn_dgram_send',`
	gen_require(`
		type innd_t;
	')

	allow $1 innd_t:unix_dgram_socket sendto;
')


########################################
## <summary>
##	Execute inn in the inn domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`inn_domtrans',`
	gen_require(`
		type innd_t, innd_exec_t;
	')

	corecmd_search_bin($1)
	domain_auto_trans($1,innd_exec_t,innd_t)

	allow innd_t $1:fd use;
	allow innd_t $1:fifo_file rw_file_perms;
	allow innd_t $1:process sigchld;
')

