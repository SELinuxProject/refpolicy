## <summary>Filesystem automounter service.</summary>

########################################
## <summary>
##	Execute automount in the automount domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`automount_domtrans',`
	gen_require(`
		type automount_t, automount_exec_t;
	')

	corecmd_search_sbin($1)
	domain_auto_trans($1, automount_exec_t, automount_t)

	allow $1 automount_t:fd use;
	allow automount_t $1:fd use;
	allow automount_t $1:fifo_file rw_file_perms;
	allow automount_t $1:process sigchld;

')

########################################
## <summary>
##	Execute automount in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`automount_exec_config',`
	gen_require(`
		type automount_etc_t;
	')

	corecmd_search_sbin($1)
	can_exec($1,automount_etc_t)
')

########################################
## <summary>
##	Allow the domain to read state files in /proc.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to allow access.
##	</summary>
## </param>
#
interface(`automount_read_state',`
	gen_require(`
		type automount_t;
	')

	allow $1 automount_t:dir search_dir_perms;
	allow $1 automount_t:file r_file_perms;
')

########################################
## <summary>
##	Do not audit attempts to get the attributes
##	of automount temporary directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`automount_dontaudit_getattr_tmp_dirs',`
	gen_require(`
		type automount_tmp_t;
	')

	dontaudit $1 automount_tmp_t:dir getattr;
')
